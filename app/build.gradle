plugins {
    id ("com.android.application")
    id ("kotlin-android")
    id ("kotlin-kapt")
    id ("androidx.navigation.safeargs.kotlin")
}


android {
    compileSdk 31


    defaultConfig {
        applicationId "com.tromian.game.afproject"
        minSdk 21
        targetSdk 31
        versionCode 2
        versionName "1.0.1"
        resConfigs "en", "ru"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

        }

    }


    buildTypes.each {
        File secretPropsFile = project.rootProject.file('local.properties')
        if (secretPropsFile.exists()) {
            Properties properties = new Properties()
            properties.load(secretPropsFile.newDataInputStream())
            def tmdbApiKey = properties.getProperty("tmdb_api_key", "")
            it.buildConfigField 'String', "TMDB_API_KEY", tmdbApiKey
        } else {
            // Use system environment variables
            def tmdbApiKey = System.getenv('TMDB_APIKEY')
            it.buildConfigField 'String', "TMDB_API_KEY", tmdbApiKey
        }

    }
}


dependencies {

    def lifecycle_version = "2.3.1"
    def room_version = "2.3.0"
    def retrofit2_version = "2.9.0"
    def navigationVersion = "2.4.0-alpha09"
    def paging_version = "3.0.1"
    def dagger_version = "2.39.1"

    //Dagger
    implementation "com.google.dagger:dagger:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"

    // Paging Library
    implementation "androidx.paging:paging-runtime-ktx:$paging_version"

    // Navigation Components
    implementation "androidx.navigation:navigation-fragment-ktx:$navigationVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$navigationVersion"

    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"

    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.1'

    // Room
    implementation "androidx.room:room-runtime:$room_version"
    implementation("androidx.room:room-ktx:$room_version")
    kapt "androidx.room:room-compiler:$room_version"

    implementation 'com.squareup.okhttp3:okhttp:5.0.0-alpha.2'
    implementation 'com.squareup.okhttp3:logging-interceptor:5.0.0-alpha.2'

    //Retrofit2
    implementation "com.squareup.retrofit2:retrofit:$retrofit2_version"
    implementation "com.jakewharton.retrofit:retrofit2-kotlin-coroutines-adapter:0.9.2"
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'


    // ViewModel
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.fragment:fragment-ktx:1.3.6"
    // LiveData
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.3.1"
    
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.3.1"


    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.3.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.2-native-mt'

    //Glide
    implementation 'com.github.bumptech.glide:glide:4.12.0'
    kapt 'com.github.bumptech.glide:compiler:4.12.0'

    implementation "org.jetbrains.kotlin:kotlin-reflect:1.5.31"
}